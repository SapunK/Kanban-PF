allprojects {
    group 'com.kanban'

    project.version '1.0'
    project.ext.set('release', '0')
    project.ext.set('outputDir', '/data/build/kanban/')

    project.version = project.hasProperty('ci') ? project.version + '.' + "$project.release" : project.version + "-SNAPSHOT"
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    //This plugin helps deploy to wildfly via eclipse
    apply plugin: 'eclipse-wtp'
    apply plugin: 'groovy'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {
        mavenCentral()
    }


    dependencies {
        implementation group: 'javax', name: 'javaee-api', version: '7.0'

        testImplementation group: 'junit', name: 'junit', version: '4.12'
        testImplementation group: 'org.mockito', name: 'mockito-core', version: '2.12.0'
        testImplementation 'org.glassfish.jersey.core:jersey-common:2.22.2'
        testImplementation group: 'org.glassfish', name: 'javax.json', version: '1.1.4'
    }

    configurations {
        compile.exclude module: 'dom4j'
    }
}

ext.getGitHashCode = {
    new ByteArrayOutputStream().withStream { os ->
        exec {
            executable = 'git'
            args = ['rev-parse', '--short', 'HEAD']
            standardOutput = os
        }

        return os.toString().trim()
    }
}


